/****** Object:  Function [dbo].[fn_ВахтыРейсы]    Committed by VersionSQL https://www.versionsql.com ******/

create FUNCTION [fn_ВахтыРейсы](@sys_id varchar(32))
RETURNS TABLE 
AS
RETURN 
(

	SELECT       ВАХТЫ.Код, ВАХТЫ.вНомер, ВАХТЫ.вДата, ВАХТЫ.вПериод, ВАХТЫ.вКонтрагент, ВАХТЫ.вДоговор, ВАХТЫ.вФИО
	, ВАХТЫ.вДатаСбора, ВАХТЫ.вЗачем, ВАХТЫ.вКогда, ВАХТЫ.вНомерРейса, ВАХТЫ.вОткуда, ВАХТЫ.вКуда, ВАХТЫ.вУчасток
	, ВАХТЫ.вПримечанияСтроки, ВАХТЫ.вДолжностьБМТсорт, ВАХТЫ.вДолжностьБМТ, ВАХТЫ.УсловияНаймаТ

	, РЕЙСЫ.рНомер, РЕЙСЫ.рДата, РЕЙСЫ.рОстановка1, РЕЙСЫ.рОстановка2
	, РЕЙСЫ.рТС, РЕЙСЫ.рМаршрут, РЕЙСЫ.вНомерБорта
	, РЕЙСЫ.рНомер AS рНомерРейса
	, УДО.пДатаАВИА


	FROM            (

			SELECT        а.ВахтаНомер AS рНомер, а.ДатаНачало AS рДата
			, а.Вахта, б.Сотрудник, у1.Название AS рОстановка1, у2.Название AS рОстановка2
			, а.ТС as рТС, а.Маршрут as рМаршрут, а.НомерБорта as вНомерБорта
			FROM				с_Рейсы AS а 
			INNER JOIN			с_РейсыСтроки AS б ON а.SYS_ID = б.Операция 
			INNER JOIN			с_РейсыПодрейсы AS о1 ON б.Остановка1 = о1.SYS_ID AND а.SYS_ID = о1.Операция 
			INNER JOIN			с_РейсыПодрейсы AS о2 ON б.Остановка2 = о2.SYS_ID AND а.SYS_ID = о2.Операция 
			LEFT OUTER JOIN		У_УС AS у2 ON о2.Куда = у2.SYS_ID 
			LEFT OUTER JOIN		У_УС AS у1 ON о1.Откуда = у1.SYS_ID
			WHERE        (а.SYS_U IS NULL) AND (б.SYS_U IS NULL) AND (а.Вахта=@sys_id)



	) AS РЕЙСЫ RIGHT OUTER JOIN (



			SELECT        с_Вахты.SYS_ID, с_Вахты.Код, с_Вахты.Номер AS вНомер, с_Вахты.Дата AS вДата, с_Вахты.ДатаНачало AS вДатаНачало
			, DATENAME(month,с_Вахты.ДатаНачало) + '-' + DATENAME(month, с_Вахты.ДатаКонец) AS вПериод, с_ВахтыСтроки.Зачем AS вЗачем
			, с_ВахтыСтроки.ДатаСбора AS вДатаСбора, с_ВахтыСтроки.Когда AS вКогда, с_ВахтыСтроки.НомерРейса AS вНомерРейса
			, ISNULL(Контрагенты.ОфНазвание, Контрагенты.Название) AS вКонтрагент
			, ISNULL(Договора.Название, '') + ISNULL(' от ' + CONVERT(varchar(10), Договора.Дата, 104), '') + ISNULL(' № ' + Договора.Номер, '') AS вДоговор
			, с_ВахтыСтроки.Сотрудник, ISNULL(Сотрудники.Фамилия, '') + ISNULL(' ' + Сотрудники.Имя, '') + ISNULL(' ' + Сотрудники.Отчество, '') AS вФИО
			, У_УС.Название AS вОткуда, У_УС_1.Название AS вКуда
			, CASE с_ВахтыСтроки.Зачем WHEN 'На вахту' THEN У_УС_1.Название WHEN 'С вахты' THEN У_УС.Название ELSE с_ВахтыСтроки.Зачем END AS вУчасток
			, с_ВахтыСтроки.Примечания AS вПримечанияСтроки, Должности.Сортировка AS вДолжностьБМТсорт, Должности.Название AS вДолжностьБМТ
			, Справочники.Название AS УсловияНаймаТ
			FROM				Должности 
			RIGHT OUTER JOIN	Сотрудники 
			INNER JOIN			с_ВахтыСтроки ON Сотрудники.SYS_ID = с_ВахтыСтроки.Сотрудник 
			LEFT OUTER JOIN		Справочники ON Сотрудники.УсловияНайма = Справочники.SYS_ID ON Должности.SYS_ID = Сотрудники.Должность 
			LEFT OUTER JOIN		У_УС AS У_УС_1 ON с_ВахтыСтроки.Куда = У_УС_1.SYS_ID 
			LEFT OUTER JOIN		У_УС ON с_ВахтыСтроки.Откуда = У_УС.SYS_ID 
			RIGHT OUTER JOIN	с_Вахты ON с_ВахтыСтроки.Операция = с_Вахты.SYS_ID 
			LEFT OUTER JOIN		Контрагенты ON с_Вахты.Заказчик = Контрагенты.SYS_ID 
			LEFT OUTER JOIN		Договора ON с_Вахты.Договор = Договора.SYS_ID
			WHERE        (с_Вахты.SYS_U IS NULL) AND (с_ВахтыСтроки.SYS_U IS NULL) AND (с_Вахты.SYS_ID = @sys_id)	
					   
	) AS ВАХТЫ ON РЕЙСЫ.Вахта = ВАХТЫ.SYS_ID AND РЕЙСЫ.Сотрудник = ВАХТЫ.Сотрудник


	left outer join (

			SELECT			ПротоколыСтроки.Сотрудник, max(Протоколы.ДатаСлед) as пДатаАВИА
			FROM            Протоколы 
			INNER JOIN		ПротоколыСтроки ON Протоколы.SYS_ID = ПротоколыСтроки.Операция
			WHERE			(Протоколы.Протокол = 'ПРР (вертолёты)') AND (Протоколы.SYS_U IS NULL) AND (ПротоколыСтроки.SYS_U IS NULL) AND (ПротоколыСтроки.Результат = 'Сдал')
					/*	and (Протоколы.ДатаСлед)>TV('ДатаНачало') */
			group by		ПротоколыСтроки.Сотрудник /*, ПротоколыСтроки.НомУдостоверения, Протоколы.ДатаСлед*/

	) as УДО on ВАХТЫ.Сотрудник=УДО.Сотрудник AND isnull(ВАХТЫ.вДатаНачало,ВАХТЫ.вДата)<=УДО.пДатаАВИА
)